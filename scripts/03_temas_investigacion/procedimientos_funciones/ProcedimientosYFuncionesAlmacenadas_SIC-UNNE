/*********************************************
TEMA 1: PROCEDIMIENTOS Y FUNCIONES ALMACENADAS
PROYECTO: SIC - UNNE
*********************************************/


----------------------------------------------
-----------BLOQUE 1: PROCEDIMIENTOS-----------
----------------------------------------------

-- USUARIO --

-- INICIAR SESIÓN

CREATE OR ALTER PROCEDURE sp_iniciar_sesion
    @login NVARCHAR(100),      -- puede ser correo o documento
    @contrasena NVARCHAR(50)
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE 
        @id_usuario INT,
        @correo NVARCHAR(100),
        @documento INT,
        @nombre NVARCHAR(100),
        @apellido NVARCHAR(100),
        @rol NVARCHAR(30),
        @estado BIT;

    -- Caso correo
    IF EXISTS (SELECT 1 FROM Usuario WHERE correo = @login)
    BEGIN
        SELECT 
            @id_usuario = id_usuario,
            @correo = correo,
            @documento = documento,
            @nombre = nombre,
            @apellido = apellido,
            @rol = rol,
            @estado = estado
        FROM Usuario
        WHERE correo = @login 
          AND contrasena = @contrasena;
    END
    ELSE 

    -- Caso documento (solo si es numérico)
    IF ISNUMERIC(@login) = 1
    BEGIN
        SELECT 
            @id_usuario = id_usuario,
            @correo = correo,
            @documento = documento,
            @nombre = nombre,
            @apellido = apellido,
            @rol = rol,
            @estado = estado
        FROM Usuario
        WHERE documento = CAST(@login AS INT)
          AND contrasena = @contrasena;
    END
    ELSE
    BEGIN
        SELECT 
            'ERROR' AS tipo,
            'El dato ingresado no es un correo válido ni un documento numérico.' AS mensaje;
        RETURN;
    END

    -- VALIDACIÓN 2: existe usuario con esas credenciales
    IF @id_usuario IS NULL
    BEGIN
        SELECT 
            'ERROR' AS tipo,
            'Correo/Documento o contraseña incorrectos.' AS mensaje;
        RETURN;
    END

    -- VALIDACIÓN 3: el usuario debe estar activo
    IF @estado = 0
    BEGIN
        SELECT 
            'ERROR' AS tipo,
            'El usuario está inactivo. Comuníquese con un verificador o administrador.' AS mensaje;
        RETURN;
    END

    -- CONFIRMACIÓN
    SELECT
        'OK' AS tipo,
        'Inicio de sesión exitoso.' AS mensaje,
        @id_usuario AS id_usuario,
        @nombre AS nombre,
        @apellido AS apellido,
        @correo AS correo,
        @documento AS documento,
        @rol AS rol;
END;
GO



-- CREAR USUARIO

CREATE OR ALTER PROCEDURE sp_crear_usuario
    @nombre       NVARCHAR(100),
    @apellido     NVARCHAR(100),
    @documento    INT,
    @correo       NVARCHAR(100),
    @contrasena   NVARCHAR(50)
AS
BEGIN
    SET NOCOUNT ON;

    -- INICIO DE TRANSACCIÓN
    BEGIN TRAN;


    -- VALIDACIÓN 1: inexistencia del correo
    IF EXISTS (SELECT 1
    FROM Usuario
    WHERE correo = @correo)
    BEGIN
        RAISERROR ('El correo ingresado ya está registrado.', 16, 1);
        ROLLBACK TRAN;
        RETURN;
    END

    -- VALIDACIÓN 2: inexistencia del documento
    IF EXISTS (SELECT 1
    FROM Usuario
    WHERE documento = @documento)
    BEGIN
        RAISERROR ('El documento ingresado ya está registrado.', 16, 1);
        ROLLBACK TRAN;
        RETURN;
    END

    -- Asignación automática del rol (luego el administrador debe cambiar el rol si fuera necesario)
    DECLARE @rolAsignado NVARCHAR(30) = 'Estudiante';


    -- INICIO DE TRANSACCIÓN
    BEGIN TRY

        INSERT INTO Usuario
        (nombre, apellido, documento, correo, contrasena, estado, rol)
    VALUES
        (@nombre, @apellido, @documento, @correo, @contrasena, 1, @rolAsignado);

        -- CONFIRMACIÓN
        COMMIT TRAN;

    END TRY
    BEGIN CATCH

        -- ERROR (se revierte)
        ROLLBACK TRAN;

        -- Reenvía el mensaje de error original de SQL Server
        DECLARE @ErrorMessage NVARCHAR(4000) = ERROR_MESSAGE();
        RAISERROR(@ErrorMessage, 16, 1);
    END CATCH
END;
GO


-- ACTUALIZAR USUARIO (permite la modificacion de los datos del usuario si asi lo quisiera)

CREATE OR ALTER PROCEDURE sp_actualizar_usuario
    @id_usuario            INT,
    @nombre                NVARCHAR(100) = NULL,
    @apellido              NVARCHAR(100) = NULL,
    @documento             INT = NULL,
    @correo                NVARCHAR(100) = NULL,
    @contrasena_actual     NVARCHAR(50) = NULL,
    @contrasena_nueva      NVARCHAR(50) = NULL
AS
BEGIN
    SET NOCOUNT ON;

    -- VALIDACIÓN 1: existencia del usuario
    IF NOT EXISTS (SELECT 1 FROM Usuario WHERE id_usuario = @id_usuario)
    BEGIN
        RAISERROR('El usuario no existe.', 16, 1);
        RETURN;
    END


    -- VALIDACIÓN 2: contraseña actual (opcional)
    IF @contrasena_nueva IS NOT NULL AND @contrasena_nueva <> ''
    BEGIN
        DECLARE @contrasena_bd NVARCHAR(50);

        SELECT @contrasena_bd = contrasena
        FROM Usuario
        WHERE id_usuario = @id_usuario;

        IF @contrasena_actual IS NULL OR @contrasena_actual = ''
        BEGIN
            RAISERROR('Debe ingresar la contraseña actual para poder cambiarla.', 16, 1);
            RETURN;
        END

        IF @contrasena_bd <> @contrasena_actual
        BEGIN
            RAISERROR('La contraseña actual es incorrecta.', 16, 1);
            RETURN;
        END
    END


    -- VALIDACIÓN 3: correo único (opcional)
    IF @correo IS NOT NULL
    BEGIN
        IF EXISTS (
            SELECT 1 FROM Usuario
            WHERE correo = @correo AND id_usuario <> @id_usuario
        )
        BEGIN
            RAISERROR('El correo ingresado ya pertenece a otro usuario.', 16, 1);
            RETURN;
        END
    END


    -- VALIDACIÓN 4: documento único (opcional)
    IF @documento IS NOT NULL
    BEGIN
        IF EXISTS (
            SELECT 1 FROM Usuario
            WHERE documento = @documento AND id_usuario <> @id_usuario
        )
        BEGIN
            RAISERROR('El documento ingresado ya pertenece a otro usuario.', 16, 1);
            RETURN;
        END
    END


    -- UPDATE PRINCIPAL (solo campos ingresados)
    BEGIN TRY
        BEGIN TRAN;

        UPDATE Usuario
        SET
            nombre =
                CASE WHEN @nombre IS NOT NULL THEN @nombre ELSE nombre END,
            apellido =
                CASE WHEN @apellido IS NOT NULL THEN @apellido ELSE apellido END,
            documento =
                CASE WHEN @documento IS NOT NULL THEN @documento ELSE documento END,
            correo =
                CASE WHEN @correo IS NOT NULL THEN @correo ELSE correo END,
            contrasena =
                CASE WHEN @contrasena_nueva IS NOT NULL AND @contrasena_nueva <> ''
                     THEN @contrasena_nueva
                     ELSE contrasena END
        WHERE id_usuario = @id_usuario;
        
        -- CONFIRMACIÓN
        COMMIT TRAN;
    END TRY
    
    -- ERROR (se revierte)
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRAN;
        
        -- Reenvía el mensaje de error original de SQL Server
        DECLARE @ErrorMessage NVARCHAR(4000) = ERROR_MESSAGE();
        RAISERROR(@ErrorMessage, 16, 1);
    END CATCH
END;
GO



-- CAMBIAR ROL (permitido solo para administradores)

CREATE PROCEDURE sp_cambiar_rol
    @id_admin   INT,
    @id_usuario INT,
    @nuevo_rol  NVARCHAR(30)
AS
BEGIN
    SET NOCOUNT ON;

    BEGIN TRY
        BEGIN TRAN;

        -- VALIDACIÓN 1: que sea administrador
        DECLARE @rol_admin NVARCHAR(30);

        SELECT @rol_admin = rol
        FROM Usuario
        WHERE id_usuario = @id_admin
          AND estado = 1;

        IF @rol_admin IS NULL
        BEGIN
            RAISERROR('El usuario que ejecuta la acción no existe o está inactivo.', 16, 1);
            RETURN;
        END

        IF @rol_admin <> 'Administrador'
        BEGIN
            RAISERROR('Solo los administradores pueden cambiar roles.', 16, 1);
            RETURN;
        END

        -- VALIDACIÓN 2: rol válido
        IF @nuevo_rol NOT IN ('Administrador', 'Estudiante', 'Verificador')
        BEGIN
            RAISERROR('El rol indicado no es válido.', 16, 1);
            RETURN;
        END

        -- VALIDACIÓN 3: un admin no puede cambiar su propio rol
        IF @id_admin = @id_usuario
        BEGIN
            RAISERROR('Un administrador no puede cambiar su propio rol.', 16, 1);
            RETURN;
        END

        -- VALIDACIÓN 4: que el usuario exista y esté activo
        DECLARE @rol_actual NVARCHAR(30);

        SELECT @rol_actual = rol
        FROM Usuario
        WHERE id_usuario = @id_usuario
          AND estado = 1;

        IF @rol_actual IS NULL
        BEGIN
            RAISERROR('El usuario a modificar no existe o está inactivo.', 16, 1);
            RETURN;
        END

        -- VALIDACIÓN 5: si ya tiene ese rol → no cambiar
        IF @rol_actual = @nuevo_rol
        BEGIN
            RAISERROR('El usuario ya posee ese rol.', 16, 1);
            RETURN;
        END

        -- UPDATE PRINCIPAL
        UPDATE Usuario
        SET rol = @nuevo_rol
        WHERE id_usuario = @id_usuario;

        -- CONFIRMACIÓN
        COMMIT;

        PRINT 'Rol actualizado correctamente.';
    END TRY


    -- ERROR (se revierte)
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK;

        -- Reenvía el mensaje de error original
        DECLARE @msg NVARCHAR(4000) = ERROR_MESSAGE();
        RAISERROR(@msg, 16, 1);
        RETURN;
    END CATCH
END
GO

-- BAJA ESTUDIANTE

CREATE OR ALTER PROCEDURE sp_baja_estudiante
    @id_usuario INT               
AS
BEGIN
    SET NOCOUNT ON;

    BEGIN TRY
        BEGIN TRAN;

        -- VALIDACIÓN 1: existencia del usuario
        IF NOT EXISTS (SELECT 1 FROM Usuario WHERE id_usuario = @id_usuario)
        BEGIN
            RAISERROR('El usuario no existe.', 16, 1);
            ROLLBACK;
            RETURN;
        END

        -- VALIDACIÓN 2: solo estudiantes pueden usar este SP
        DECLARE @rol VARCHAR(30);
        SELECT @rol = rol FROM Usuario WHERE id_usuario = @id_usuario;

        IF @rol <> 'estudiante'
        BEGIN
            RAISERROR('Solo los estudiantes pueden darse de baja a sí mismos.', 16, 1);
            ROLLBACK;
            RETURN;
        END

        -- VALIDACIÓN 3: solo puede pasar de 1 → 0
        DECLARE @estadoActual BIT;
        SELECT @estadoActual = estado FROM Usuario WHERE id_usuario = @id_usuario;

        IF @estadoActual = 0
        BEGIN
            RAISERROR('La cuenta ya está dada de baja.', 16, 1);
            ROLLBACK;
            RETURN;
        END

        -- UPDATE PRINCIPAL
        UPDATE Usuario
        SET estado = 0
        WHERE id_usuario = @id_usuario;

        -- CONFIRMACIÓN
        COMMIT;
    END TRY

    -- ERROR (se revierte)
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK;
        
        -- Reenvía el mensaje de error original
        DECLARE @error NVARCHAR(4000) = ERROR_MESSAGE();
        RAISERROR(@error, 16, 1);
    END CATCH
END
GO


-- ACTIVAR USUARIO (solo para verificador)

CREATE OR ALTER PROCEDURE sp_activar_usuario
    @id_verificador   INT,
    @id_usuario       INT
AS
BEGIN
    SET NOCOUNT ON;

    -- VALIDACIÓN 1: que el verificador exista y esté activo
    DECLARE @rol_verificador NVARCHAR(30);

    SELECT @rol_verificador = rol
    FROM Usuario
    WHERE id_usuario = @id_verificador AND estado = 1;

    IF @rol_verificador IS NULL
    BEGIN
        RAISERROR('El usuario que ejecuta la acción no existe o está inactivo.', 16, 1);
        RETURN;
    END

    IF @rol_verificador <> 'Verificador'
    BEGIN
        RAISERROR('Solo los usuarios con rol Verificador pueden activar cuentas.', 16, 1);
        RETURN;
    END

    -- VALIDACIÓN 3: existencia del usuario a activar
    DECLARE @estado_usuario BIT;

    SELECT @estado_usuario = estado
    FROM Usuario
    WHERE id_usuario = @id_usuario;

    IF @estado_usuario IS NULL
    BEGIN
        RAISERROR('El usuario que se intenta activar no existe.', 16, 1);
        RETURN;
    END

    -- VALIDACIÓN 4: usuario ya activo → no activar
    IF @estado_usuario = 1
    BEGIN
        RAISERROR('El usuario ya está activo.', 16, 1);
        RETURN;
    END

    -- UPDATE PRINCIPAL
    BEGIN TRY
        BEGIN TRAN;

        UPDATE Usuario
        SET estado = 1
        WHERE id_usuario = @id_usuario;

        -- CONFIRMACIÓN
        COMMIT TRAN;

        PRINT 'El usuario ha sido activado correctamente.';
    END TRY
    
    -- ERROR (se revierte)
    BEGIN CATCH
        IF @@TRANCOUNT > 0
            ROLLBACK TRAN;

        -- Reenvía el mensaje de error original
        DECLARE @ErrorMessage NVARCHAR(4000) = ERROR_MESSAGE();
        RAISERROR(@ErrorMessage, 16, 1);
    END CATCH
END;
GO


-- BUSCAR USUARIO

CREATE OR ALTER PROCEDURE sp_consultar_usuario
    @id_usuario   INT = NULL,
    @documento    INT = NULL,
    @correo       NVARCHAR(100) = NULL
AS
BEGIN
    SET NOCOUNT ON;

    -- VALIDACIÓN: campo a buscar existente
    IF @id_usuario IS NULL AND @documento IS NULL AND @correo IS NULL
    BEGIN
        RAISERROR('Debe proporcionar al menos un parámetro: ID, documento o correo.', 16, 1);
        RETURN;
    END

    -- BÚSQUEDA POR ID 
    IF @id_usuario IS NOT NULL
    BEGIN
        SELECT *
        FROM Usuario
        WHERE id_usuario = @id_usuario;
        RETURN;
    END

    -- BÚSQUEDA POR DOCUMENTO 
    IF @documento IS NOT NULL
    BEGIN
        SELECT *
        FROM Usuario
        WHERE documento = @documento;
        RETURN;
    END

    -- BÚSQUEDA POR CORREO
    IF @correo IS NOT NULL
    BEGIN
        SELECT *
        FROM Usuario
        WHERE correo = @correo;
        RETURN;
    END

    -- BÚSQUEDA POR APELLIDO  
    IF @apellido IS NOT NULL
    BEGIN
        SELECT *
        FROM Usuario
        WHERE apellido = @apellido;
        RETURN;
    END
END;
GO


-- LISTAR USUARIOS (FILTRO, BUSQUEDA, ORDEN, PAGINACIÓN)

CREATE OR ALTER PROCEDURE sp_listar_usuarios
(
    @pagina          INT = 1,                  -- número de página
    @tamanoPagina    INT = 10,                 -- cantidad por página
    @buscar          NVARCHAR(100) = NULL,     -- texto de búsqueda
    @rol             NVARCHAR(30) = NULL,      -- filtro por rol
    @estado          INT = NULL,               -- filtro por estado
    @orden           NVARCHAR(20) = 'apellido' -- campo para ordenar
)
AS
BEGIN
    SET NOCOUNT ON;

    -- VALIDACIÓN DE PARÁMETROS
    IF @pagina < 1 SET @pagina = 1;
    IF @tamanoPagina < 1 SET @tamanoPagina = 10;

    IF @orden NOT IN ('nombre', 'apellido', 'correo', 'documento', 'rol', 'estado')
        SET @orden = 'apellido'; -- orden por defecto

    -- ARMADO DE FILTROS
    DECLARE @offset INT = (@pagina - 1) * @tamanoPagina;

    -- CONSULTA DINÁMICA CON FILTROS Y BÚSQUEDA
    ;WITH UsuariosFiltrados AS 
    (
        SELECT 
            id_usuario,
            nombre,
            apellido,
            correo,
            documento,
            rol,
            estado
        FROM Usuario
        WHERE 
            (@rol IS NULL OR rol = @rol)
            AND (@estado IS NULL OR estado = @estado)
            AND (
                    @buscar IS NULL
                    OR nombre    LIKE '%' + @buscar + '%'
                    OR apellido  LIKE '%' + @buscar + '%'
                    OR correo    LIKE '%' + @buscar + '%'
                    OR documento LIKE '%' + @buscar + '%'
                )
    )

    -- DEVOLVER DATOS PAGINADOS
    SELECT 
        *
    FROM UsuariosFiltrados
    ORDER BY 
        CASE WHEN @orden = 'nombre' THEN nombre END,
        CASE WHEN @orden = 'apellido' THEN apellido END,
        CASE WHEN @orden = 'correo' THEN correo END,
        CASE WHEN @orden = 'documento' THEN documento END,
        CASE WHEN @orden = 'rol' THEN rol END,
        CASE WHEN @orden = 'estado' THEN estado END
    OFFSET @offset ROWS
    FETCH NEXT @tamanoPagina ROWS ONLY;

    -- TOTAL DE REGISTROS FILTRADOS (para frontend)
    SELECT COUNT(*) AS total_registros
    FROM Usuario
    WHERE 
        (@rol IS NULL OR rol = @rol)
        AND (@estado IS NULL OR estado = @estado)
        AND (
                @buscar IS NULL
                OR nombre    LIKE '%' + @buscar + '%'
                OR apellido  LIKE '%' + @buscar + '%'
                OR correo    LIKE '%' + @buscar + '%'
                OR documento LIKE '%' + @buscar + '%'
            );
END
GO


-- ESTUDIANTE --

-- LISTAR TODOS LOS ESTUDIANTES






